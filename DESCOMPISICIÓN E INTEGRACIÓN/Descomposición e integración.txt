/*
Tecnológico de Costa Rica
Curso de Simulación 
Tarea programada de Descomposición e Integración
Fabian Martínez Valverde 2019061575
Gustavo Blanco Alfaro 2019063503


*/


/*
* Genera la lista de los puntos iniciales de los intervalos
*
* Input: limInf n h
* Output: lista
*/
newcmd generarIntervalos limInf n h lista

	temp = limInf	' El primer valor es el límite inferior
	lista = ()

	repeat n	' repite la cantidad de intervalos que se ingresaron
		concat lista temp lista	' Agrega el punto a una lista
		temp = temp + h		' Aumente el punto en h
	end

end


'*****
' Una función de Prueba Muy Sencilla
' en [0,3[
newcmd fun x y
	y = (x^2)/9
end



newcmd simpson a b nSimpson fx

	h = (b - a) / nSimpson	' Calcula el h para el intervalo

	res = fun(a) + fun(a + nSimpson*h)	' Prepara la variable de las supas con los extremos de los fx porque estos se multimplican por 1
	cont = 1	' Inicia un contador con 1

	numCiclos = nSimpson - 1	' Prepara los números de cliclos restándole el último valor porque ya se sumó al res

	repeat numCiclos
		x = a + cont*h		' Calcula el x para evaluar en el fx

		temp = fun(x)		' Calcula fx
		remainder cont 2 mod	' Saca el módulo del contador con 2, para que cuando contador sea par sea 0 y cuando sea impar sea 1
		res = res + (2 + 2*mod) * temp	' Calcula la multimplicación de 2 o 4 con el fx y lo aumenta al total
							' La lógica aquí para el 4 y el 2 es que se suma 2 + 2*residuo, así cuando el residuo es 0 da 2 y cuando es 1 da 4

		cont = cont + 1
	
	end

	fx = (h/3)*res		' Calcula la estumación de la integral


end



newcmd generarFx lista n h nSimpson fx ptos
	
	' Inicializa las listas
	ptos = ()
	fx = ()

	foreach inf lista	' Recorre la lista de valores iniciales
		sup = inf + h	' Calcula el valor superior de ese intervalo
		simpson inf sup nSimpson res	' Usa el algoritmo de simpson 
		pto = inf + (sup - inf)/2	' Calcula el punto medio de ese intervalo

		' Agrega los valores a las listas
		concat fx res fx
		concat ptos pto ptos
	end

end



newcmd acumularLista fx fxAcum

	contador = 0
	fxAcum = ()

	foreach f fx	' Recorre los fx sumándolos y calculando los acumulados
		contador = contador + f
		concat fxAcum contador fxAcum
	end

end

newcmd transformadaInversa fxAcum ptos r x
	limiteInf = 0
	limiteSup = 0
	intervalo = 1
	foreach limiteSup fxAcum
		if r >= limiteInf AND r < limiteSup
			take ptos intervalo x
		end
		intervalo = intervalo + 1
		limiteInf = limiteSup
	end
end

newcmd generarVar fxAcum ptos tamañoMuestra listaX
	uniform tamañoMuestra 0 1 listaNum
	foreach r listaNum
		transformadaInversa fxAcum ptos r x
		concat x listaX listaX
	end
	print listaX
end

/***********************************************************************
			MAIN
***********************************************************************/

clearoutput
/*
input "Límite Inferior :" limInf
input "Límite Superior :" limSup
input "Número intervalos :" n
input "Particiones Simpson (número par):" nSimpson
input "Tamaño de Muestra :" tamañoMuestra
*/

limInf = 0
limSup = 3
n = 3
nSimpson = 4
tamañoMuestra = 10000

h = (limSup - limInf) / n	' Calcula las divisiones de la función


generarIntervalos limInf n h lista	' Obtiene la lista de los valores iniciales de los intervalos 


generarFx lista n h nSimpson fx ptos	' Calcula los fx y los puntos medios de los intervalos 
print ""
print table fx%4.4F ptos

acumularLista fx fxAcum 
print ""
print table fxAcum%4.4F ptos

print

generarVar fxAcum ptos tamañoMuestra listaX 
HISTOGRAM binsize h listaX










