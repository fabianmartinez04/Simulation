

/*
Tecnológico de Costa Rica
Curso de Simulación 
Tarea programada de Huecos con Dígitos
Fabian Martínez Valverde 2019061575
Gustavo Blanco Alfaro 2019063503

Valor obtenido con los datos de prueba de n=30 y 4 decimales: 5.18054
Valor obtenido con los datos de prueba de n=1000 y 4 decimales: 33.5402
*/

/*Librerias*/
INCLUDE "/lib/coordinatedVectorCommands.txt" 'Import para sortcoord
INCLUDE "/lib/unsortedSearchCommands.txt"



/*
* Explotar un numero
* Convierte un número en una lista
*
* Input: dec (num decimales), num
* Output: lista
*/
newcmd explotar dec num lista
   decMasUno = dec + 1
   res = ()
   t = num * 10^decMasUno
   /* integer round t t */
   integer truncate t t
   /*
   print num 
   print t
   */
   numCiclos = decMasUno
   repeat numCiclos
      rem = t % 10
      quo = t / 10
      integer truncate quo quo
      t = quo
      concat rem res res
   end
   take res 1,dec lista
end




/*
* Incrementa los contadores que ya están trabajando
*
* Input: contadores
* Output: contadores
*/
newcmd incContadores contadores
   foreach i 1, 10	'El indice para recorrer los 10 contadores
      
      take contadores i cont	'Toma el valor del contador
      if cont <> -1		'Si es diferente a -1 (si está trabajando) lo incrementa
         cont = cont+1
         put cont i contadores
      end
      
   end
end


/*
* Cuenta los huecos de una lista de digitos
*
* Input: lista
* Output: keys values
*/
newcmd contar lista keys values
   
   contadores = (-1 -1 -1 -1 -1 -1 -1 -1 -1 -1) 	'Contadores, contadores[i] con i = digito
					'El -1 significa que aún no está trabajando, o sea que aún no aparece el primer dígito para empezar a contar
   keys = ()	'Huecos
   values = () 	'Cantidad huecos
   
   
   
   foreach n lista
      n = n+1	'Como las listas empiezan a contar en 1 se incrementa n en 1
      
      take contadores n contador	'Toma el contador de del dígito n
      
      if contador <> -1		'Si el contador está activo entonces guarda el registro
         
         count keys=contador existeLlave	'Obtiene si ya existe la llave
         
         if existeLlave = 0		'Si no existe, la agrega y coloca una repetición
            concat keys contador keys
            concat values (1) values

         else			'Si existe:
            SEARCH_UNSORTED keys contador index	'Obtiene el índice en la lista de llaves
            take values index repeticiones	'Obtiene el valor de la lista de repeticiones
            repeticiones = repeticiones+1		'Aumenta la repetición en 1
            put repeticiones index values		'Aumenta el valor en la lista
         end
         
      end
      
      incContadores contadores		'Incrementa los contadores activos
      put (0) n contadores		'Reinicia el contador del digito actual
      
   end
   
   sortcoord keys values	'Ordena la lista
   'print keys values
end






/*
* Calcula las frecuencias esperadas
*
* Input: keys total
* Output: frecuencias
*/
newcmd calcEsperados keys total frecuencias
   
   frecuencias = ()	'Inicializa la lista vacía
   
   foreach key keys	'Recorre la lista de llaves
      res = 0.1*(0.9^key)*total	'Calcula p(key)
      if res < 5			'Si la frecuencia esperada es menor a 5 se termina
         break
      end
      concat frecuencias res frecuencias	'Si no, lo agrega a la lista
      
   end
end







/*
*********************************************
                     Main
*********************************************
*/

clearoutput

read file "" entrada	'Lee el archivo con los números
input cantDecimales 'Lee la cantidad de decimales para los números 

lista = () 'Inicializa la lista vacía para los números


' Recorre la lista de números y los explota para agregarlos a la lista final 
foreach numero entrada
   
   explotar cantDecimales numero res
   concat lista res lista
   
end

print lista
print


contar lista keys fo	'Cuenta la lista para obtener la lista de los huecos

sum fo totalHuecos	'Calcula el total de huecos

calcEsperados keys totalHuecos fe	'Calcula las frecuencias esperadas

size fe n	'Obtiene el tamaño de los esperados

take keys 1,n bin		'Recorta las listas de llaves y valores al mismo tamaño de las esperadas
take fo 1,n fo

print table bin%2.0f fo%2.0f fe	'Imprime bonita la tabla

CHISQUARE fo fe chiCuadrado 	'Calcula chisquare y lo imprime
print
print chiCuadrado




