/*
Tecnológico de Costa Rica
Curso de Simulación 
Tarea programada de Prueba de Poker
Fabian Martínez Valverde 2019061575
Gustavo Blanco Alfaro 2019063503
Resultados:
Archivo de n=30: 4.2813
Archivo de n=1000: 1.4589
*/


/*
* Explotar un numero
* Convierte un número en una lista
*
* Input: dec (num decimales), num
* Output: lista
*/
newcmd explotar dec num lista
   decMasUno = dec + 1
   res = ()
   t = num * 10^decMasUno
   /* integer round t t */
   integer truncate t t
   /*
   print num 
   print t
   */
   numCiclos = decMasUno
   repeat numCiclos
      rem = t % 10
      quo = t / 10
      integer truncate quo quo
      t = quo
      concat rem res res
   end
   take res 1,dec lista
end



/*
* Devuelve el índice de la categoría de las cartas
* 1 = Diferentes
* 2 = Un par
* 3 = Dos pares
* 4 = Tercia
* 5 = Full
* 6 = Póker
* 7 = Quintilla
*
* Input = cartas
* Output = categoría
*/
newcmd categorizar cartas categoriaN
   
   multiples cartas=2 res
   if res = 1	' Si hay un par
      multiples cartas=3 res

      if res=1	' Si hay Poker
         categoriaN = 5

      else		' Si hay un par
         categoriaN = 2
      end

   elseif res = 2	' Si hay dos pares
      categoriaN = 3

   else
      multiples cartas=1 res
      if res = 5	' Si todos son diferentes
         categoriaN = 1

      else
         multiples cartas=3 res
         if res = 1	' Si hay una tercia
            categoriaN = 4

         else
            multiples cartas=4 res
            if res = 1	' Si hay un Full
               categoriaN = 6

            else		' Si hay una Quintilla
               categoriaN = 7
            end
         end
      end
   end
   
end


/*
* Aumenta el contador de la categoría
*
* Input = categorias, categoria
* Output = categorias
*/
newcmd incCategoria categorias categoria
	take categorias categoria temp
	temp = temp+1
	put temp categoria categorias
end


/*
* Calcula las frecuencias esperadas
*
* Input: categorias
* Output: frecuencias
*/
newcmd calcEsperados categorias frecuencias

	sum categorias total    'Obtiene el total de numeros
	probabilidades = (0.3024 0.5040 0.1080 0.0720 0.0090 0.0045 0.0001) 'Probabilidades para cada ocurrencia con 5 digitos decimales
	MULTIPLY probabilidades total frecuencias 'Multiplica cada probabilidad por el total de numeros

end



/*
*********************************************
                     Main
*********************************************
*/

clearoutput

read file "" entrada	'Lee el archivo con los números
categorias = (0 0 0 0 0 0 0)	'Inicializa los contadores de las categorias


' Recorre la lista de números y los explota para agregarlos a la lista final 
foreach numero entrada
   
   explotar 5 numero cartas 		' Obtiene las cartas en una lista
   categorizar cartas categoria	' Obtiene la categoría de las cartas
   
  incCategoria categorias categoria	'Incrementa el contador según la categoría
   
end

calcEsperados categorias fe	'Calcula las frecuencias esperadas 

/*Imprime la tabla de forma organizada y el valor de chi cuadrado*/
NAME Diferentes Par DosPares Tercia Full Poker Quintilla
clases = (Diferentes Par DosPares Tercia Full Poker Quintilla)
copy categorias fo 
WRITE window heading clases fo%5.4F fe%2.4F	

CHISQUARE categorias fe chiCuadrado 	'Calcula chisquare y lo imprime
print
print chiCuadrado

