/*
Tecnológico de Costa Rica
Curso de Simulación 
Tarea programada de Inventario con Colas
Fabian Martínez Valverde 2019061575
Gustavo Blanco Alfaro 2019063503

Solución con:

3 empleados:
4 empleados:
5 empleados:
6 empleados:

*/

'Declaración de nombres usados
NAME Inicio Salida Entrada Fin
NAME 9999 x

'Declara los nombres globales para usarlos en funciones
GLOBAL Inicio
GLOBAL Salida
GLOBAL Entrada
GLOBAL Fin
GLOBAL x


'Función para obtener la cantidad de camiones en espera al inicio de la simulación
newcmd obtenerCamionesEspera cantidad
	copy 50#0 25#1 15#2 10#3 tabla
	sample 1 tabla cantidad
end



'Función para obtener la media según la cantidad de empleados
newcmd obtenerMedia empleados media
	if empleados = 3
		media = 37
	elseif empleados = 4
		media = 35
	elseif empleados = 5
		media = 33
	elseif empleados = 6
		media = 31
	else
		media = -1
	end
end


'Función para obtener la siguiente entrada de un camión
newcmd obtenerEntrada tiempo
   exponential 1 40 tiempo
   integer ceiling tiempo tiempo
end


'Función para obtener la salida de un camión según la cantidad de empleados
newcmd obtenerSalida promedio tiempo
   exponential 1 promedio tiempo
   integer ceiling tiempo tiempo
end


'Función que incrementa todos los elementos en la cola el tiempo dado
newcmd incTiempoCola tiempo cola
	nuevaCola = ()
	foreach c cola
		c = c + tiempo
		concat nuevaCola c nuevaCola
	end
	cola = nuevaCola
end


'Función que incrementa o decrementa el contador de camiones
newcmd calcContCamiones evento cont
	if evento = Salida
		cont = cont - 1
	elseif evento = Entrada
		cont = cont + 1
	end
end


'Función que imprime todos los datos de una iteración (una fila de la tabla)
newcmd printRow TiempoActual EventoActual SalActual EntActual T_OciosoActual T_Esp_BusActual ColaActual
	'Print de cada iteración
	String "" ColaActual%1.0f Cola
		
	'Si ya no se aceptan entradas se imprime la marca de que ya no se aceptan (estético)
	if EntActual > 480
		output "%8.4F\t%8\t\t%8.4F\t\t%3.4F**\t\t%8.4F\t\t%8.4F\t\t" TiempoActual EventoActual SalActual EntActual T_OciosoActual T_Esp_BusActual
	else
		output "%8.4F\t%8\t\t%8.4F\t\t%8.4F\t\t%8.4F\t\t%8.4F\t\t" TiempoActual EventoActual SalActual EntActual T_OciosoActual T_Esp_BusActual
	end
	print Cola
end





/***********************************************************************
			MAIN
***********************************************************************/

clearoutput

input "Cantidad de empleados: " empleados

obtenerMedia empleados media

if media = -1
	print "Los empleados deben ser de 3 a 6"
else

	obtenerCamionesEspera contCamiones 'Obtiene los camiones que estarán al inicio
	print contCamiones
	print "\n"


	'Preparación de las variables para los costos
	salarios = 0
	tiempo_normal = 0
	tiempo_extra = 0

	tiempo_ocioso = 0
	operacion_almacen = 0
	espera_del_camion = 0

	costo_total = 0


	'Preparación de variables


	TiempoActual = 0
	TiempoAnterior = 0
	TiempoDif = 0
	EventoActual = Inicio
	SalActual = 0
	SalAnterior = 0
	EntActual = 0
	T_OciosoActual = 0
	T_Esp_BusActual = 0

	
	'Preparación de variables contadores para los costos
	ocioTotal = 0
	esperaTotal = 0

	camionesEnLista = contCamiones - 1
	copy camionesEnLista#0 ColaActual 'Inicia la lista de los camiones en la cola

	print "Tiempo\t\tEvento\t\tSig.Salida\tSig.Entrada\tT.Ocioso\tT.Espera\tCola"
	print "=============================================================================================================="
	
	'Repetir hasta que la próxima entrega sea luego del minuto 480 y que ya no hayan camiones en la cola
	until EntActual >= 480 AND contCamiones <= 1

		'Aumenta o disminuye el contador de camiones según el evento
		calcContCamiones EventoActual contCamiones


		'Calcula el tiempo transcurrido
		TiempoDif = TiempoActual - TiempoAnterior

		

		T_Esp_BusActual = 0 'Pone el tiempo de espera en 0 cada vez para evitar datos basura


		'Si el evento es diferente a salida entonces quiere decir que se calcula una nueva entrada
		if EventoActual <> Salida
			obtenerEntrada EntActual
			EntActual = TiempoActual + EntActual
		elseif contCamiones >= 1	'Si el evento es una salida y el contador de camiones es mayor a 1 entonces saca uno de la cola
			take ColaActual 1 T_Esp_BusActual
			T_Esp_BusActual = T_Esp_BusActual + TiempoDif
			esperaTotal = esperaTotal + T_Esp_BusActual
			remove ColaActual 1 ColaActual

		end


		'Si en la iteración anterior no habían camiones para una salida aumenta el tiempo ocioso
		if SalActual = 9999
			T_OciosoActual = TiempoDif
			ocioTotal = ocioTotal + T_OciosoActual
		else
			T_OciosoActual = 0
		end


		
		


		'Para obtener otra salida pregunta si la vez pasada hubo tiempo ocioso o que si el evento es una salida y hay camiones para sacar
		if SalActual = x OR (contCamiones > 0 AND EventoActual <> Entrada)
			obtenerSalida media SalActual
			SalActual = TiempoActual + SalActual
		elseif contCamiones = 0	'Si lo anterior no se cumple y no hay camiones para atender se pone tiempo ocioso, no hay salidas próximas
			SalActual = x
		end


		'Incrementa los tiempos de espera de la cola
		incTiempoCola TiempoDif ColaActual

		'Si el evento es una entrada y ya hay un camión siendo atendido entonces se pone en la cola
		if EventoActual = Entrada AND contCamiones > 1
			concat ColaActual 0 ColaActual
		end

		
		'Asigna las variables para la iteración anterior
		TiempoAnterior = TiempoActual
		salAnterior = salActual


		'Imprime la fila de la iteración
		printRow TiempoActual EventoActual SalActual EntActual T_OciosoActual T_Esp_BusActual ColaActual



		
		'Calcula cual acción es la más cercana a ocurrir, si una entrada o una salida
		if EntActual < SalActual AND EntActual <= 480
			TiempoActual = EntActual
			EventoActual = Entrada
		else
			TiempoActual = SalActual
			EventoActual = Salida

		end
		
		'print contCamiones

	end

	'Terminada la simulación realiza la última salida con el evento "Fin" y calcula la última información
	EventoActual = Fin

	'Si se terminó antes de tiempo entonces los minutos restantes cuenta como tiempo ocioso
	if TiempoActual < 480
		T_OciosoActual = 480 - TiempoActual
		TiempoActual = 480
	end

	'Imprime la última información
	output "%8.4F\t%8\t\t\t\t\t\t%8.4F\t\t\t\t\n" TiempoActual EventoActual T_OciosoActual


	
	print "=============================================================================================================="
	print "Tiempo\t\tEvento\t\tSig.Salida\tSig.Entrada\tT.Ocioso\tT.Espera\tCola"
	print "\n"
	print "==================="
	print "      COSTOS       "
	print "==================="


	'Calcula todos los costos de la simulación
	tiempo_normal = empleados * (480/60) * 25
	tiempo_extra = empleados * (( TiempoActual - 480 ) / 60 ) * 37.5
	salarios = tiempo_normal + tiempo_extra

	tiempo_ocioso = empleados * (ocioTotal / 60) * 25

	operacion_almacen = (TiempoActual / 60) * 500

	espera_del_camion = (esperaTotal / 60) * 100

	costo_total = salarios + tiempo_ocioso + operacion_almacen + espera_del_camion

	print salarios%8.4f tiempo_ocioso%8.4f operacion_almacen%8.4f espera_del_camion%8.4f costo_total%8.4f


end






