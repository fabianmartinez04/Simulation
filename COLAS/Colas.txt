/*
Tecnológico de Costa Rica
Curso de Simulación 
Tarea programada de Inventario con Colas
Fabian Martínez Valverde 2019061575
Gustavo Blanco Alfaro 2019063503

Solución con:

3 empleados:
4 empleados:
5 empleados:
6 empleados:

*/


'Función para obtener la cantidad de camiones en espera al inicio de la simulación
newcmd obtenerCamionesEspera cantidad
	copy 50#0 25#1 15#2 10#3 tabla
	sample 1 tabla cantidad
end



'Función para obtener la media según la cantidad de empleados
newcmd obtenerMedia empleados media
	if empleados = 3
		media = 37
	elseif empleados = 4
		media = 35
	elseif empleados = 5
		media = 33
	elseif empleados = 6
		media = 31
	else
		media = -1
	end
end


'Función para obtener la siguiente entrada de un camión
newcmd obtenerEntrada tiempo
   exponential 1 40 tiempo
   integer ceiling tiempo tiempo
end


'Función para obtener la salida de un camión según la cantidad de empleados
newcmd obtenerSalida promedio tiempo
   exponential 1 promedio tiempo
   integer ceiling tiempo tiempo
end



newcmd incTiempoCola tiempo cola

	nuevaCola = ()

	foreach c cola
		c = c + tiempo
		concat nuevaCola c nuevaCola
	end

	cola = nuevaCola

end


/***********************************************************************
			MAIN
***********************************************************************/

clearoutput

input "Cantidad de empleados: " empleados

obtenerMedia empleados media

if media = -1
	print "Los empleados deben ser de 3 a 6"
else

	obtenerCamionesEspera contCamiones
	print contCamiones
	print "\n"


	'Preparación de variables
	NAME Inicio Salida Entrada Fin
	NAME 9999 x

	TiempoActual = 0
	TiempoAnterior = 0
	TiempoDif = 0
	EventoActual = Inicio
	SalActual = 0
	SalAnterior = 0
	EntActual = 0
	T_OciosoActual = 0
	T_Esp_BusActual = 0

	tamColaActual = 0

	camionesEnLista = contCamiones - 1
	copy camionesEnLista#0 ColaActual

	print "Tiempo\t\tEvento\t\tSig.Salida\tSig.Entrada\tT.Ocioso\tT.Espera\tCola"
	print "===================================================================================================="
	
	'Repetir hasta que la próxima entrega sea luego del minuto 480 y que ya no hayan camiones en la cola
	until EntActual >= 480 AND contCamiones <= 1


		if EventoActual = Salida
			contCamiones = contCamiones - 1
		elseif EventoActual = Entrada
			contCamiones = contCamiones + 1
		end


		size ColaActual tamColaActual


		TiempoDif = TiempoActual - TiempoAnterior

		

		'Si el evento es diferente a salida entonces quiere decir que se calcula una nueva entrada
		if EventoActual <> Salida
			obtenerEntrada EntActual
			EntActual = TiempoActual + EntActual
			T_Esp_BusActual = 0
		elseif contCamiones >= 1
			take ColaActual 1 T_Esp_BusActual
			T_Esp_BusActual = T_Esp_BusActual + TiempoDif
			remove ColaActual 1 ColaActual
		else
			T_Esp_BusActual = 0
		end


		'Si en la iteración anterior no habían camiones para una salida aumenta el tiempo ocioso
		if SalActual = 9999
			T_OciosoActual = TiempoDif
		else
			T_OciosoActual = 0
		end


		
		


		
		if SalActual = x OR (contCamiones > 0 AND EventoActual <> Entrada)
			obtenerSalida media SalActual
			SalActual = TiempoActual + SalActual
		
		elseif contCamiones = 0
			SalActual = x
		end


		
		

		incTiempoCola TiempoDif ColaActual

		if EventoActual = Entrada AND contCamiones > 1
			concat ColaActual 0 ColaActual
		end

		
		'Asigna las variables para la iteración anterior
		TiempoAnterior = TiempoActual
		salAnterior = salActual


		'Print de cada iteración
		String "" ColaActual%1.0f Cola
		
		
		if EntActual > 480
			output "%8.4F\t%8\t\t%8.4F\t\t%3.4F**\t\t%8.4F\t\t%8.4F\t\t" TiempoActual EventoActual SalActual EntActual T_OciosoActual T_Esp_BusActual
		else
			output "%8.4F\t%8\t\t%8.4F\t\t%8.4F\t\t%8.4F\t\t%8.4F\t\t" TiempoActual EventoActual SalActual EntActual T_OciosoActual T_Esp_BusActual
		end

		print Cola
		'print contCamiones



		
		'Calcula cual acción es la más cercana a ocurrir, si una entrada o una salida
		if EntActual < SalActual AND EntActual <= 480
			TiempoActual = EntActual
			EventoActual = Entrada

			
		else
			TiempoActual = SalActual
			EventoActual = Salida

		end
		
		'print contCamiones

		
		

	end

	EventoActual = Fin

	if TiempoActual < 480
		T_OciosoActual = 480 - TiempoActual
		TiempoActual = 480
	end

	output "%8.4F\t%8\t\t\t\t\t\t%8.4F\t\t\t\t\n" TiempoActual EventoActual T_OciosoActual


	
	print "===================================================================================================="
	print "Tiempo\t\tEvento\t\tSig.Salida\tSig.Entrada\tT.Ocioso\tT.Espera\tCola"

end






