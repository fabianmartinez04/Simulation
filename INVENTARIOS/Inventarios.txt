/*
Tecnológico de Costa Rica
Curso de Simulación 
Tarea programada de Inventarios
Fabian Martínez Valverde 2019061575
Gustavo Blanco Alfaro 2019063503

Costo total de q=50 y r=20
Ejemplo de una corrida: 189300.0
*/

/*
En la busqueda del q y r optimos con 10 <= q <= 100 y 10 <= r <= q, se obtuvo la siguiente tabla al realizarla con 365 dias de inventario y unas 100000 veces para cada combinacion de posibles valores mostrada. Luego, se promedió el costo total de esas 100000 corridas y dicho promedio es el presentado en la siguiente tabla:

q/r	10         	20          	30          	40          	50          	60          	70          	80          	90		100         
100     314897.03     	370462.84	438969.36	510849.81	583206.49	655835.92	728831.19	802031.67	875489.56	950287.31
90      280300.71 	334237.94   	402498.69 	474510.02 	547030.65 	619740.65 	692878.04 	766256.30 	840864.73     
80      246360.60 	298283.75 	366164.32   	438278.36 	510928.62 	583845.33 	657094.56 	731428.97    
70      213407.22 	262546.45 	330023.27 	402141.37 	474962.26 	548078.12 	622175.28        
60      181815.51 	227246.21 	294072.94 	366337.30 	439295.53 	513108.14       
50      152400.94 	192552.10 	258349.19 	330742.08 	404326.97     
40      126768.07 	159003.45 	223220.75 	296020.96       
30      108625.94 	127418.15 	189231.04       
20      118456.43 	101087.004     
10      6522447.27 

Por lo tanto los valores optimos son:
q = 20
r = 20
*/


/*
* Obtiene una nueva venta (de 0 a 14)
*/
newcmd newVenta res
	res = 15

	until res <= 14
		poisson 1 7 res
	end
end


/*
* Obtiene el tiempo para un nuevo pedido, solo si no hay otro activo
*/
newcmd newPedido dia
	if dia = 0
		dias = (1 1 1 2 2 2 2 3 3 3)
		sample 1 dias dia
	end
end


/*
* Calcula en base al q y r si hay que realizar un nuevo pedido
*/
newcmd hayQuePedir q r dia res
	if q <= r
		newPedido dia
		res = 1
	else
		res = 0
	end
end




/*
* El programa principal
*/
newcmd main q r costoTotal
	
	'Preparación de las variables

	'Prepara las listas para la tabla final
	Ini = ()	'La lista del inventario
	PedirQ = ()	'La lista de las solicitudes
	dias = ()	'La lista de los días restantes hasta una entrega
	Venta = ()	'La lista de las ventas de cada día
	

	'Prepara las variables de uso en cada iteración
	qActual = q	'La variable de la cantidad de productos en el día actual
	solicitud = 0	'La variable de si en el día actual hay una solicitud de entrega activa (1=sí 0=no)
	dia = 0		'La variable de la cantidad de días que faltan en el día actual
	VentaDia = 0	'La variable de las ventas del día actual

	
	'Prepara las variables de los costos	
	costoTotal = 0
	costoFijo = 0
	costoInventario = 0
	costoPenalizacion = 0

	numCiclos = 365	'La cantidad de días que se hará la prueba

	N = 1,numCiclos	'Prepara la lista de números para la tabla


	'Realiza la simulación del inventario los días que se definieron
	repeat numCiclos


		'Hace los cálculos para saber si hay que hacer un pedido, esto actualiza la variable "día" y "solicitus"
		'es decir que actualiza los días para realizar un pedido y la variable que dice si hay un pedido en proceso
		hayQuePedir qActual r dia solicitud


		newVenta VentaDia	'Obtiene la venta del día


		'Agrega algunos datos del día a las listas totales
		concat dias dia dias	
		concat Ini qActual Ini
		concat PedirQ solicitud PedirQ
		concat Venta VentaDia Venta



		'Si el día es > 0 (si hay espera de encargos) se disminuye la espera en un día
		if dia > 0
			dia = dia - 1
		end


		'Se calcula el inventario que queda después de las ventas del día
		'se reutiliza la variable del inventario del día porque de igual manera se usa en esa misma variable para el próximo día
		qActual = qActual - VentaDia

		'Agrega el inventario al final del día a la lista
		concat	Final qActual Final


		'Calcula los costos del día
		if qActual > 0		'Si el inventario al final del día es positivo, entonces es costo de inventario
			costoInventario = costoInventario + 20*qActual
		elseif qActual < 0	'Si el inventario al final del día es negativo, entonces es costo de penalización
			costoPenalizacion = costoPenalizacion + -50*qActual
		end


		'Pregunta si una entrega llega el día siguiente, de ser así aumenta el inventario y cobra el costo fijo
		if dia = 0 and solicitud = 1
			qActual = qActual + q
			costoFijo = costoFijo + 100
		end


	end

	print table N%8.4F Ini%8.4F PedirQ%8.4F dias%8.4F Venta%8.4F Final%8.4F
	print
	print costoFijo costoInventario costoPenalizacion

	costoTotal = costoFijo + costoInventario + costoPenalizacion
	
end





/***********************************************************************
			MAIN
***********************************************************************/

clearoutput

input "Ingrese q: " q
input "Ingrese r: " r

print 

'Llama al programa principal con los datos del usuario
main q r costoTotal

print
print costoTotal














